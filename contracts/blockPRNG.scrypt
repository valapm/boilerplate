import "blockchain.scrypt";

/*
  A trustless pesudo-random number generator using the block containing the deployed contract tx as an entropy source
*/
contract BlockchainPRNG {

    // maximal target for any block to be considered valid
    int blockchainTarget;
    PubKey alice;
    PubKey bob;

    /*
      @bh: header of the block containing the contract tx/UTXO
      @merkleproof: Merkle proof for the tx
      @sig: winner signature
     */
    public function bet(BlockHeader bh, MerkleProof merkleproof, Sig sig,  SigHashPreimage txPreimage) {
        require(Tx.checkPreimage(txPreimage));

        // get id of previous tx
        Sha256 prevTxid = Sha256(SigHash.outpoint(txPreimage)[:32]);

        // validate block header
        require(Blockchain.isBlockHeaderValid(bh, this.blockchainTarget));

        // verify previous tx is in the block
        require(Blockchain.txInBlock(prevTxid, bh, merkleproof));

        // use block header's nonce's last bit as a pesudo-random number
        PubKey winner = bh.nonce % 2 ? this.alice : this.bob;
        require(checkSig(sig, winner));
    }
}
